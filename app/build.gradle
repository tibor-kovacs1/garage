plugins {
    id 'com.android.application'
}

android {
    namespace 'com.example.garage'
    compileSdk 34

    defaultConfig {
        applicationId "com.example.garage"
        minSdk 26
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = [appAuthRedirectScheme: 'com.example.garage']

        Properties properties = new Properties()
        File propertiesFile = project.file('credentials.properties')
        if (!propertiesFile.exists()) {
            properties.setProperty('issuer', '')
            properties.setProperty('redirectUri', '')
            properties.setProperty('clientId', '')
            properties.setProperty('postLogoutUri', '')

            Writer writer = new FileWriter(propertiesFile, false)
            properties.store(writer, 'Change these variables to be able use garage app')
            writer.close()
        }

        properties.load(propertiesFile.newDataInputStream())
        resValue "string", "ISSUER", properties.getProperty("issuer")
        resValue "string", "REDIRECT_URI", properties.getProperty("redirectUri")
        resValue "string", "CLIENT_ID", properties.getProperty("clientId")
        resValue "string", "POST_LOGOUT_URI", properties.getProperty("postLogoutUri")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    //todo use strivacity sdk lib from external package
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}